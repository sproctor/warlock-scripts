# Usage: .braid <disposal container>
#	based on Aveda's Amazing Braiding Script
#	Updated on 3/2/2013 by sproctor

if_1 goto CHECKEXP

echo How to use:
echo .braid <place you're going to stick all these ropes>
exit

CHECKEXP:
	getcomponent mech exp mechanical lore
	if %mech contains "lock" then goto WAITEXP
	goto BRAID

FORAGE_PAUSE:
pause
FORAGE:
	match FORAGE_PAUSE ...wait
	match CHECKEXP You manage to find some grass.
	match FORAGE Roundtime:
	match PUT You really need to have
	put forage grass
	matchwait

BRAID_PAUSE:
pause
BRAID:
	match BRAID_PAUSE ...wait
	match FORAGE You need to have more material
	match FORAGE You need to be holding
	match PULL mistake
	match PULL You fumble
	match PUT wasted effort.
	match PUT You need both hands
	match CHECKEXP Roundtime:
	put braid my grass
	matchwait

PULL_PAUSE:
pause
PULL:
	match FORAGE You need to have more material
	match PULL_PAUSE ...wait
	match PUT rope
	match PUT You tug
	match PUT I'm afraid that you
	put pull my grass
	matchwait

PUT_PAUSE:
pause
PUT:
	match PUT_PAUSE ...wait
	match FORAGE rope
	match DUMP What were you referring to?
	match DUMP I'm afraid you
	put put my rope in %1
	matchwait

DUMP_PAUSE:
pause
DUMP:
	match DUMP_PAUSE ...wait
	match FORAGE You drop some
	match FORAGE What were you referring to?
	match FORAGE You put your grass
	put put my grass in %1
	put put my grass in %1
	matchwait

WAITEXP:
	echo Waiting for exp to absorb
	pause 30
	goto CHECKEXP
