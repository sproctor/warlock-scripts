#######################################
### Aveda's Amazing Braiding Script ###
###      Last updated 6/01/2008     ###
### http://dr.aveda.googlepages.com ###
###                                 ###
###     To operate, just type:      ###
###    .braid <disposal container>  ###
#######################################

if_1 goto test

echo How to use:
echo .braid <place you're going to stick all these ropes>
exit

test:
	match end mind lock
	match forage Overall state of
	put skill ML
	matchwait
forage_pause:
pause
forage:
	match forage_pause ...wait
	match braid You manage to find some grass.
	match forage Roundtime:
	match put You really need to have
	put forage grass
	matchwait
braid_pause:
pause
braid:
	match braid_pause ...wait
	match forage You need to have more material
	match pull mistake
	match put wasted effort.
	match put You need both hands
	match braid Roundtime:
	put braid my grass
	matchwait
pull_pause:
pause
pull:
	match forage You need to have more material
	match pull_pause ...wait
	match put rope
	match put You tug
	match put I'm afraid that you
	put pull my grass
	matchwait
put_pause:
pause
put:
	match put_pause ...wait
	match test rope
	match dump What were you referring to?
	match dump I'm afraid you
	put put my rope in %1
	matchwait
dump_pause:
pause
dump:
	match dump_pause ...wait
	match test You drop some
	match test What were you referring to?
	match test You put your grass
	put put my grass in %1
	put put my grass in %1
	matchwait
end:
echo ==========DONE==============
